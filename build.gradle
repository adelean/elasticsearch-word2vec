group = 'com.adelean.word2vec'
version = '0.4.0-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        mavenLocal()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'gradle.plugin.com.dorongold.plugins:task-tree:1.4'
        classpath 'gradle.plugin.services.bst:weave-dependencies:0.1.5'
        classpath 'org.elasticsearch.gradle:build-tools:6.5.4'
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.9.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:latest.release'
    }
}

apply plugin: 'com.dorongold.task-tree'
apply plugin: 'java'
apply plugin: 'services.bst.weave-dependencies'
apply plugin: 'idea'
apply plugin: 'elasticsearch.esplugin'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.jfrog.artifactory'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

test.enabled = false
integTestRunner.enabled = false

Task carrotTest = tasks.findByPath('test')
tasks.register("unitTests", Test) {
    useJUnitPlatform()
    classpath = carrotTest.classpath
}

publishing {
    publications {
        PluginPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            artifact bundlePlugin
            groupId project.group
            artifactId 'elasticsearch-word2vec-plugin'
            version project.version
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_API_KEY')
    pkg {
        repo = 'elasticsearch-word2vec'
        name = 'elasticsearch-word2vec'
        userOrg = 'adelean'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/adelean/elasticsearch-word2vec.git'
        version {
            name = project.version
            released  = new Date()
            vcsTag = 'v' + project.version
        }
    }
    publications = ['PluginPublication', 'ModelUploaderPublication']
}

artifactory {
    contextUrl = 'https://oss.jfrog.org'
    resolve {
        repository {
            repoKey = 'libs-release'
        }
    }
    publish {
        repository {
            if (project.version.endsWith("-SNAPSHOT")) {
                repoKey = 'oss-snapshot-local'
            } else {
                repoKey = 'oss-release-local'
            }

            username = System.getenv('BINTRAY_USER')
            password = System.getenv('BINTRAY_API_KEY')
        }
        defaults {
            publications 'PluginPublication', 'ModelUploaderPublication'
        }
    }
}

weaveDependencies {
    aspectJVersion = '1.9.5'
    classesToExclude = []
    ajcXlint = 'ignore'
}

def aspectsConfig = project.configurations.getByName('aspects')
def weaveConfig = project.configurations.getByName('weave')
project.configurations.getByName('compile').setExtendsFrom([])
project.configurations.getByName('implementation').extendsFrom(aspectsConfig, weaveConfig)

project.afterEvaluate {
    def preweaveDeps = getDirectPaths(aspectsConfig)

    project.sourceSets.each { SourceSet sourceSet ->
        sourceSet.runtimeClasspath = sourceSet.runtimeClasspath.filter {
            !preweaveDeps.contains(it.absolutePath)
        }

        sourceSets.test.runtimeClasspath = sourceSets.test.runtimeClasspath.filter {
            !preweaveDeps.contains(it.absolutePath)
        }
    }

    def jarHellTask = project.tasks.getByName('jarHell');
    jarHellTask.classpath = project.sourceSets.test.runtimeClasspath
}

project.tasks.getByName('bundlePlugin').dependsOn(project.tasks.getByName('jarHell'))

private Set<String> getDirectPaths(Configuration config) {
    Set<String> depSet = [] as Set
    config.resolvedConfiguration.firstLevelModuleDependencies.each { ResolvedDependency topDependency ->
        topDependency.moduleArtifacts.each { ResolvedArtifact artifact ->
            depSet.add(artifact.file.absolutePath)
        }
    }
    return depSet
}

ext {
    licenseFile = rootProject.file('LICENSE')
    noticeFile = rootProject.file('NOTICE')
}

esplugin {
    name 'elasticsearch-word2vec-plugin'
    description 'Word2vec plugin for Elasticsearch'
    classname 'com.adelean.elasticsearch.word2vec.Word2VecPlugin'
}

forbiddenApisMain.enabled = false
licenseHeaders.enabled = false
dependencyLicenses.enabled = false
thirdPartyAudit.enabled = false
javadoc.enabled = false
checkstyleMain.enabled = false

forbiddenPatterns {
    exclude '**/*.bin'
}

def transitiveDependenciesGroups = [ 'org.deeplearning4j', 'org.nd4j' ] as String[]
def mandatoryDependenciesGroups = [ 'org.aspectj' ] as String[]

/* Disable compile failure on warnings */
project.afterEvaluate {
    project.tasks.withType(JavaCompile).configureEach({ JavaCompile compileTask ->
        options.compilerArgs.remove('-Werror')
    })
}

/* Solve conflicting dependencies */
project.configurations.all({ Configuration configuration ->
    configuration.resolutionStrategy {
        preferProjectModules()
    }
})

Closure isMandatoryDependency = { File file ->
    def nameParts = file.absolutePath.split('/')
    return nameParts
            .find({ part ->
                mandatoryDependenciesGroups.contains(part)
            }) != null;
}

Closure enableTransitive = { Dependency dependency ->
    def isTransitiveDependency = transitiveDependenciesGroups.any(dependency.group.&equalsIgnoreCase)
    if (isTransitiveDependency) {
        // Must be inside 'if' block to not disable transitivity of other dependencies
        dependency.transitive = true
    }
}

Closure disableTransitive = { Dependency dependency ->
    dependency.transitive = false
}

String dequalify(String jarName) {
    def tokens = jarName.replace('.jar', '').split('-')

    String names = '';
    String versions = '';

    for (token in tokens) {
        if (token.matches('\\d+\\.\\d+(\\.\\d+)?') || token.matches('(alpha|beta|SNAPSHOT)\\d+')) {
            versions += '-' + token
        } else {
            names += (names.isEmpty() ? '' : '-') + token
        }
    }

    return names + versions + '.jar';
}

void addImplementationDependencies(Project project) {
    def implementationDependencies = (project.configurations.runtimeClasspath
            - project.configurations.compileOnly
            - project.configurations.aspects
            - project.configurations.weave)

    implementationDependencies
        .forEach { dependency ->
            def newName = dequalify(dependency.name)

            project
                    .bundlePlugin
                    .from(dependency)
                    .rename(dependency.name, newName)
        }
}

void addMandatoryDependencies(Project project, Closure isMandatoryDependency) {
    project
            .configurations
            .runtimeClasspath
            .findAll(isMandatoryDependency)
            .forEach { dependency ->
                project.bundlePlugin.from(dependency)
            }
}

void addWeavedDependencies(Project project) {
    project.bundlePlugin.from('${project.buildDir}/weaved-libs/weaveDependencies') {
        include '*.jar'
    }
}

project.configurations.getByName(JavaPlugin.IMPLEMENTATION_CONFIGURATION_NAME).dependencies.all(enableTransitive)
project.configurations.getByName(JavaPlugin.TEST_COMPILE_CONFIGURATION_NAME).dependencies.all(enableTransitive)
project.configurations.getByName('aspects').dependencies.all(disableTransitive)
project.configurations.getByName('weave').dependencies.all(disableTransitive)

def ES_VERSION = '6.5.4'
def DP4J_VERSION = '1.0.0-beta7'

dependencies {
    compile group: 'org.elasticsearch', name: 'elasticsearch', version: ES_VERSION

    implementation(group: 'org.deeplearning4j', name: 'deeplearning4j-nlp', version: DP4J_VERSION) {
        exclude group: 'org.deeplearning4j', module: 'deeplearning4j-tsne'
        exclude group: 'org.deeplearning4j', module: 'deeplearning4j-ui-components'
        exclude group: 'org.deeplearning4j', module: 'deeplearning4j-util'

        exclude group: 'org.objenesis'
        exclude group: 'net.java.dev.jna'
        exclude group: 'org.nd4j', module: 'nd4j-api'
        exclude group: 'org.nd4j', module: 'nd4j-native-api'
        exclude group: 'org.nd4j', module: 'nd4j-jackson'

        exclude group: 'org.datavec', module: 'datavec-data-image'
        exclude group: 'com.github.vinhkhuc', module: 'jfasttext'

        exclude group: 'net.sf.opencsv', module: 'opencsv'
        exclude group: 'org.freemarker', module: 'freemarker'
        exclude group: 'org.threadly', module: 'threadly'
        exclude group: 'org.lucee', module: 'oswego-concurrent'

        exclude group: 'org.bytedeco', module: 'hdf5-platform'
        exclude group: 'org.bytedeco', module: 'opencv'
    }

    aspects project(':aspects')
    weave(group: 'org.nd4j', name: 'nd4j-api', version: DP4J_VERSION) {
        exclude group: 'org.objenesis'
        exclude group: 'net.java.dev.jna'
        exclude group: 'org.nd4j', module: 'nd4j-context'
    }

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.6.0'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.14.0'
}

addImplementationDependencies(project)
addWeavedDependencies(project)
addMandatoryDependencies(project, isMandatoryDependency)
